//------------------------------------------------------------------------------
//
// Copyright (c) 2016, Linaro Limited. All rights reserved.
//
// This program and the accompanying materials
// are licensed and made available under the terms and conditions of the BSD License
// which accompanies this distribution.  The full text of the license may be found at
// http://opensource.org/licenses/bsd-license.php
//
// THE PROGRAM IS DISTRIBUTED UNDER THE BSD LICENSE ON AN "AS IS" BASIS,
// WITHOUT WARRANTIES OR REPRESENTATIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED.
//
//------------------------------------------------------------------------------

#include <AsmMacroIoLibV8.h>

    AREA    |.text|,ALIGN=3,CODE,READONLY

    EXPORT ArmReplaceLiveTranslationEntry
    EXPORT ArmReplaceLiveTranslationEntrySize

#define CTRL_M_BIT      (1 << 0)

//VOID
//ArmReplaceLiveTranslationEntry (
//  IN  UINT64  *Entry,
//  IN  UINT64  Value,
//  IN  UINT64  Address
//  )
ArmReplaceLiveTranslationEntry PROC

  // disable interrupts
  mrs   x4, daif
  msr   daifset, #0xf
  isb sy

  // clean and invalidate first so that we don't clobber
  // adjacent entries that are dirty in the caches
  dc    civac, x0
  dsb   nsh

  EL1_OR_EL2_OR_EL3(x3)
1
  // disable the MMU
  mrs   x8, sctlr_el1
  bic   x9, x8, #CTRL_M_BIT
  msr   sctlr_el1, x9
  isb sy

  // write updated entry
  str   x1, [x0]

  // invalidate again to get rid of stale clean cachelines that may
  // have been filled speculatively since the last invalidate
  dmb   sy
  dc    ivac, x0

  // flush translations for the target address from the TLBs
  // tlbi    vaae1, x2
  // MU_CHANGE : Use the alternate encoding of tlbi due to the assembler not
  // generating the correct code when tlbi is used.
  sys #0, C8, C7, #3, x2
  dsb   sy

  // re-enable the MMU
  msr   sctlr_el1, x8
  isb sy
    b  %f4


2
  // disable the MMU
  mrs   x8, sctlr_el2
  bic   x9, x8, #CTRL_M_BIT
  msr   sctlr_el2, x9
  isb sy

  // write updated entry
  str   x1, [x0]

  // invalidate again to get rid of stale clean cachelines that may
  // have been filled speculatively since the last invalidate
  dmb   sy
  dc    ivac, x0

  // flush the TLBs
  tlbi  vae2, x2
  dsb   sy

  // re-enable the MMU
  msr   sctlr_el2, x8
  isb sy
    b  %f4


3
  // disable the MMU
  mrs   x8, sctlr_el3
  bic   x9, x8, #CTRL_M_BIT
  msr   sctlr_el3, x9
  isb sy

  // write updated entry
  str   x1, [x0]

  // invalidate again to get rid of stale clean cachelines that may
  // have been filled speculatively since the last invalidate
  dmb   sy
  dc    ivac, x0

  // flush the TLBs
  tlbi  vae3, x2
  dsb   sy

  // re-enable the MMU
  msr   sctlr_el3, x8
  isb sy

4
  msr   daif, x4
  ret
ArmReplaceLiveTranslationEntry ENDP

ArmReplaceLiveTranslationEntryEnd

ArmReplaceLiveTranslationEntrySize PROC
  DCD  . - ArmReplaceLiveTranslationEntry
ArmReplaceLiveTranslationEntrySize ENDP


    END